sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDirs "src/integrationTest/resources"
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test, dependsOn: 'classes') {
    group 'Verification'
    description 'Runs the integration tests.'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    binResultsDir = file("$buildDir/integrationTest-results/binary/integrationTest")
    reports.junitXml.destination = "$buildDir/integrationTest-results"
    reports.html.destination = "$buildDir/reports/integrationTest"
    systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
